(*******************************************************************************
| rectangle.o: содержит модуль rectangle с процедурами и типами, 
| осуществляющими обработку прямоугольников
*******************************************************************************)

MODULE rectangle; 
IMPORT In, Out, FileIO, figure;

(******************************************************************************)
TYPE  
  (* Структуры данных описывающие прямоугольник *)

  (* Указатель на прямоугольник *)
  Prect* = POINTER TO rect;

  (* Запись, определяющая структуру прямоугольника *)
  rect* = RECORD(figure.fig)
    x*, y* : INTEGER (* стороны прямоугольника*)
  END;               

(******************************************************************************)
(* Процедура динамического создания и инициализации нового прямоугольника *)
PROCEDURE New*(x, y: INTEGER): Prect;
VAR tmp : Prect;
BEGIN     
    NEW(tmp);            
    tmp^.x := x;
    tmp^.y := y;      
    RETURN tmp
END New; 

(******************************************************************************)
(* Процедура инициализации уже существующего прямоугольника *)
PROCEDURE  (VAR r: rect) Init*(x, y: INTEGER);
BEGIN     
    r.x := x;
    r.y := y;      
END Init; 

(******************************************************************************)
(* Процедура вычисления периметра прямоугольника - наследника фигуры *)
PROCEDURE (VAR r: rect) Perimeter* () : REAL;
VAR
BEGIN          
  RETURN (r.x + r.y) * 2
END Perimeter;

(******************************************************************************)
(* Процедура Заполнения прямоугольника из входного потока *)
(******************************************************************************)
PROCEDURE (VAR r: rect) Input*;
BEGIN     
  Out.String("x = "); In.Int(r.x); 
  Out.String("y = "); In.Int(r.y); 
END Input; 

(******************************************************************************)
(* Процедура вывода прямоугольника - наследника фигуры *)
PROCEDURE (VAR r: rect) Output*;
BEGIN          
  Out.String("Rectangle: x = "); Out.Int(r.x, 0); 
  Out.String(", y = "); Out.Int(r.y, 0); 
  Out.String(",   perimemter = "); Out.Real(r.Perimeter(), 0); 
  Out.Ln;
END Output;

(******************************************************************************)
(* Процедура Заполнения прямоугольника из файла *)
(******************************************************************************)
PROCEDURE (VAR r: rect) FileInput*(VAR inFile : FileIO.TFile);
VAR
  f : INTEGER;
BEGIN     
  f := inFile.ReadInt(r.x); 
  f := inFile.ReadInt(r.y); 
END FileInput; 

(******************************************************************************)
(* Процедура вывода в файл прямоугольника - наследника фигуры *)
PROCEDURE (VAR r: rect) FileOutput*(VAR outFile : FileIO.TFile); 
VAR 
  flag : INTEGER;
BEGIN     
  flag := outFile.WriteString("Rectangle: x = "); 
  flag := outFile.WriteInt(r.x, 7); 
  flag := outFile.WriteString(", y = "); 
  flag := outFile.WriteInt(r.y, 7); 
  flag := outFile.WriteString(",   perimemter = "); 
  flag := outFile.WriteReal(r.Perimeter(), 7); 
  flag := outFile.Ln();
END FileOutput;

(******************************************************************************)
END rectangle.
